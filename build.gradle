buildscript {
	repositories {
		jcenter()
		maven {
			name = "forge"
			url = "https://files.minecraftforge.net/maven"
		}
		maven {
			name = "SpongePowered"
			url = "http://repo.spongepowered.org/maven"
		}
	}
	dependencies {
		classpath "net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT"
		classpath "org.spongepowered:mixingradle:0.6-SNAPSHOT"
		classpath "com.github.jengelman.gradle.plugins:shadow:1.2.3"
	}
}

apply plugin: "net.minecraftforge.gradle.forge"
apply plugin: "org.spongepowered.mixin"
apply plugin: "com.github.johnrengelman.shadow"

version = modVersion
group = modGroup
archivesBaseName = modBaseName

minecraft {
	version = project.forgeVersion
	runDir = "run"
	mappings = project.mcpVersion
	makeObfSourceJar = false
	coreMod = "cookiedragon.luchadora.mixin.CoreMod"
}

mixin {
	defaultObfuscationEnv searge
	add sourceSets.main, "mixin.luchadora.refmap.json"
}

repositories {
	mavenCentral()
	jcenter()
	maven {
		name = "spongepowered-repo"
		url = "http://repo.spongepowered.org/maven/"
	}
}

dependencies {
	compile "com.mojang:brigadier:1.0.17"
	compile 'com.jagrosh:DiscordIPC:0.4'
	compile("org.spongepowered:mixin:0.7.4-SNAPSHOT") {
		exclude module: "launchwrapper"
		exclude module: "guava"
		exclude module: "gson"
		exclude module: "commons-io"
	}
}

processResources {
	// this will ensure that this task is redone when the versions change.
	inputs.property "version", project.version
	inputs.property "mcversion", project.minecraft.version

	// replace stuff in mcmod.info, nothing else
	from(sourceSets.main.resources.srcDirs) {
		include "mcmod.info"

		// replace version and mcversion
		expand "version": project.version, "mcversion": project.minecraft.version
	}

	// copy everything else, thats not the mcmod.info
	from(sourceSets.main.resources.srcDirs) {
		exclude "mcmod.info"
	}
}

jar {
	manifest {
		attributes(
				"MixinConfigs": "mixins.luchadora.json",
				"TweakClass": "org.spongepowered.asm.launch.MixinTweaker",
				"TweakOrder": 0,
				"FMLCorePluginContainsFMLMod": "true",
				"FMLCorePlugin": "cookiedragon.luchadora.mixin.CoreMod",
				"ForceLoadAsMod": "true",
		)
	}
}

shadowJar {
	dependencies {
		include(dependency("org.spongepowered:mixin"))
		include(dependency("com.mojang:brigadier:1.0.17"))
	}
	exclude "dummyThing"
	exclude "LICENSE.txt"
	baseName = "luchadora"
	version = ""
	classifier = "shadow"
}

task clientJar(type: Jar, dependsOn: jar) {
	from(zipTree(jar.archivePath)) {
		include "cookiedragon/luchadora/**"
		include "mixins.luchadora.json"
		include "META-INF/**"
		include "mcmod.info"
	}
	baseName = "luchadora"
	version = ""
	classifier = "client"
}

task dependencyJar(type: Jar, dependsOn: shadowJar) {
	from(zipTree(shadowJar.archivePath)) {
		exclude "cookiedragon/**"
		exclude "mixins.luchadora.json"
		exclude "META-INF/**"
		exclude "mcmod.info"
	}
	baseName = "luchadora"
	version = ""
	classifier = "dependencies"
}

task deleteNonUsed(type: Delete, dependsOn: shadowJar) {
	delete(fileTree("build/libs") {
		exclude "luchadora-dependencies.jar"
		exclude "luchadora-client.jar"
	})
	System.out.println("Deleting")
}

build.dependsOn(clientJar, dependencyJar, deleteNonUsed)
